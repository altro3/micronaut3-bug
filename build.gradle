plugins {
    id 'java'
    id 'idea'
    id 'io.micronaut.application' version '3.3.1'
    id 'io.micronaut.aot' version '3.3.1'
}

ext {
    jreImage = 'bellsoft/liberica-openjre-alpine-musl:17.0.2'
    jreImageNative = 'debian:stable-slim'
}

mainClassName = "com.micronaut.bug.Application"
micronaut {
    version '3.4.0'
    runtime "netty"
    testRuntime "junit5"
    enableNativeImage true
    processing {
        group project.group
        incremental true
        annotations "com.micronaut.bug.*"
    }
    aot {
        cacheEnvironment = true
        optimizeServiceLoading = true
        optimizeClassLoading = true
        convertYamlToJava = true
        precomputeOperations = true
        deduceEnvironment = false
    }
}

wrapper.gradleVersion = '7.4.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

dockerfile {
    baseImage(jreImage)
    args "-Dfile.encodig=UTF-8"
}
graalvmNative {
    binaries {
        toolchainDetection = false
        main {
            verbose = true
        }
    }
}
dockerfileNative {
    baseImage(jreImageNative)
}

dependencies {

    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "io.micronaut:micronaut-inject-java"

    compileOnly "org.projectlombok:lombok"
    compileOnly "io.micronaut:micronaut-inject-java"

    runtimeOnly "ch.qos.logback:logback-classic"

    implementation "io.micronaut:micronaut-runtime"

    implementation "io.micronaut.micrometer:micronaut-micrometer-registry-prometheus"

    // micronaut-data
//    annotationProcessor "io.micronaut.data:micronaut-data-processor"
//    runtimeOnly "io.micronaut.sql:micronaut-jdbc-hikari"
//    runtimeOnly "org.postgresql:postgresql"
//    implementation "io.micronaut.data:micronaut-data-jdbc"
//    runtimeOnly "io.micronaut.flyway:micronaut-flyway"

}

tasks.withType(JavaCompile).all {
    options.encoding = 'UTF-8'
    options.compilerArgs = [
            '-parameters',
            '-Xlint:unchecked',
            '-Xlint:deprecation'
    ]
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute(module("io.micronaut:micronaut-jackson-databind"))
                    .using(module("io.micronaut.serde:micronaut-serde-jackson:1.0.0"))
        }
    }
}

idea {

    module {
        excludeDirs += file('.idea')
        excludeDirs += file('out')
    }
}
